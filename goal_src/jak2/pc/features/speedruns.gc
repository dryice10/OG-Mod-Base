;;-*-Lisp-*-
(in-package goal)

(define-extern speedrun-practice-spawn (function none)) ;; defined at end of this file
(define-extern speedrun-practice-store-temp-checkpoint (function none)) ;; defined at end of this file
(define *practice-spawn-post-init* (the-as (function none) #f))

(define *last-real-continue-point* (the-as continue-point #f))
(defun generic-post-init ()
  (when *last-real-continue-point*
    (set! (-> *game-info* current-continue) *last-real-continue-point*)
    )
  (none)
  )

(define *speedrun-info* (new 'static 'speedrun-info))

(defmethod set-category! ((this speedrun-info) (category speedrun-category))
  (set! (-> this category) category)
  (none))

(defmethod start-run! ((this speedrun-info))
  ;; randomize game id so the autosplitter knows to restart
  (reset! *autosplit-info-jak2*)
  ;; turn on speedrun verification display
  (set! (-> this display-run-info?) #t)
  ;; ensure any required settings are enabled
  (enforce-settings! this)
  ;; finalize any category specific setup code
  (case (-> this category)
    (((speedrun-category newgame-normal))
      (initialize! *game-info* 'game (the-as game-save #f) "game-start"))
    (((speedrun-category newgame-heromode))
      (initialize! *game-info* 'game (the-as game-save #f) "game-start-hero"))
    (((speedrun-category all-cheats-allowed))
      (initialize! *game-info* 'game (the-as game-save #f) "game-start")))

  (if (!= -1 (-> *game-info* auto-save-which))
      (set! (-> *setting-control* user-default auto-save) #t))

  (none))

(defmethod enforce-settings! ((this speedrun-info))
  (set! (-> *pc-settings* ps2-actor-vis?) #t) ;; force PS2 actor visibility
  (set-frame-rate! *pc-settings* 60 #t) ;; force FPS to `60`
  ;; For posterity, the main reason why changing the cheats is useful is for two main reasons:
  ;; - If you are playing a category that requires cheats (ie. a turbo jetboard one) you'd
  ;;   probably like the game to automatically set the appropriate ones for you
  ;; - If you are playing a category that forbids cheats, you wouldn't want your run invalidated because you forgot
  ;;
  ;; However, the pc-settings stores a backup of your cheats whenever you manually modify them (NYI - no menus yet)
  ;; and when speedrunner mode is first enabled.  They are restored when speedrunner mode is disabled.
  (when (!= (-> this category) (speedrun-category all-cheats-allowed))
    ;; disable any active cheats
    (set! (-> *pc-settings* cheats) (the-as pc-cheats #x0)))
  (none))

(defmethod hide-run-info! ((this speedrun-info))
  (set! (-> this display-run-info?) #f)
  (none))

(defmethod update! ((this speedrun-info))
  "A per frame update for speedrunning related stuff"
  ;; Ensure the speedrunner menu process is enabled or destroyed
  (when (and (-> *pc-settings* speedrunner-mode?)
             (not *speedrun-menu*))
    (process-spawn speedrun-menu :init speedrun-menu-init #f :to *entity-pool*))
  (when (and (not (-> *pc-settings* speedrunner-mode?))
             *speedrun-menu*)
    (deactivate (-> *speedrun-menu* 0)))
  ;; do speedrunner mode things
  (when (-> *pc-settings* speedrunner-mode?)

    ;; call custom checkpoint post-init function, if any
    (when (and *target*
               (progress-allowed?)
               (or (not *progress-process*) (gone? (-> *progress-process* 0)))
               (>= (- (-> *display* base-clock frame-counter) (-> *game-info* blackout-time)) (seconds 0.1))
               )
      (when *practice-spawn-post-init*
        (*practice-spawn-post-init*)
        (set! *practice-spawn-post-init* #f)
        )

      (when (cpad-hold? 0 r3)
        (cond
          ((cpad-pressed? 0 up)
            (format 0 "player tried to load tmp checkpoint~%")
            (speedrun-practice-spawn)
            )
          ((cpad-pressed? 0 down)
            (format 0 "player tried to save tmp checkpoint~%")
            (speedrun-practice-store-temp-checkpoint)
            )
          )
        )
      )

    ;; Update auto-splitter struct
    (update! *autosplit-info-jak2*)
    ;; see if we should stop drawing the run info (when you escape the fortress!)
    (when (task-complete? *game-info* (game-task fortress-escape))
      (set! (-> this display-run-info?) #f))
    (when (-> this display-run-info?)
       ;; Draw info to the screen
       (draw-run-info! this))
    ;; enforce settings even if they've changed them
    (enforce-settings! this))
  (none))

(defmethod draw-run-info! ((this speedrun-info))
  "Draw speedrun related settings in the bottom left corner"
  (when (and (-> *pc-settings* speedrunner-mode?)
             (-> this display-run-info?))
    (clear *temp-string*)
    (clear *pc-encoded-temp-string*)
    (format *temp-string* "<COLOR_WHITE>Category: <COLOR_GREEN>~S~%<COLOR_WHITE>PC Cheats: <COLOR_GREEN>~D~%<COLOR_WHITE>Frame Rate: <COLOR_GREEN>~D~%<COLOR_WHITE>PS2 Actor Vis?: <COLOR_GREEN>~S~%<COLOR_WHITE>Version: <COLOR_GREEN>~S~%"
            (enum->string speedrun-category (-> this category))
            (the-as int (-> *pc-settings* cheats))
            (-> *pc-settings* target-fps)
            (if (-> *pc-settings* ps2-actor-vis?) "true" "false")
            *pc-settings-built-sha*)
    (pc-encode-utf8-string *temp-string* *pc-encoded-temp-string*)
    (with-dma-buffer-add-bucket ((buf (-> (current-frame) global-buf)) (bucket-id debug-no-zbuf1))
      ;; reset bucket settings prior to drawing - font won't do this for us, and
      ;; draw-raw-image can sometimes mess them up. (intro sequence)
      (dma-buffer-add-gs-set-flusha buf (alpha-1 (new 'static 'gs-alpha :b #x1 :d #x1)) (tex1-1 (new 'static 'gs-tex1 :mmag #x1 :mmin #x1)))
      (let ((font-ctx (new 'stack 'font-context *font-default-matrix* 510 365 0.0 (font-color default) (font-flags right shadow kerning large))))
        (set! (-> font-ctx scale) 0.325)
        (draw-string-adv *pc-encoded-temp-string* buf font-ctx))))
  (none))

;; Speedrun Menu

(define *speedrun-popup-menu*
  (new 'static 'popup-menu
    :entries (new 'static 'boxed-array :type popup-menu-entry
      (new 'static 'popup-menu-button :label "Reset" :on-press (lambda () (send-event (ppointer->process *speedrun-menu*) 'invoke (speedrun-menu-command reset))))
      (new 'static 'popup-menu-label :label "Categories")
      (new 'static 'popup-menu-flag :label "Normal"
        :on-press (lambda () (set-category! *speedrun-info* (speedrun-category newgame-normal)))
        :is-toggled? (lambda () (= (-> *speedrun-info* category) (speedrun-category newgame-normal))))
      (new 'static 'popup-menu-flag :label "Hero Mode"
        :on-press (lambda () (set-category! *speedrun-info* (speedrun-category newgame-heromode)))
        :is-toggled? (lambda () (= (-> *speedrun-info* category) (speedrun-category newgame-heromode))))
      (new 'static 'popup-menu-flag :label "All Cheats Allowed"
        :on-press (lambda () (set-category! *speedrun-info* (speedrun-category all-cheats-allowed)))
        :is-toggled? (lambda () (= (-> *speedrun-info* category) (speedrun-category all-cheats-allowed))))
      (new 'static 'popup-menu-button :label "Exit" :on-press (lambda () (send-event (ppointer->process *speedrun-menu*) 'invoke (speedrun-menu-command exit))))
      )
    )
  )

(define *speedrun-menu* (the-as (pointer speedrun-menu) #f))

(defbehavior speedrun-menu-init speedrun-menu ()
  (set! *speedrun-menu* (the-as (pointer speedrun-menu) (process->ppointer self)))
  (set! (-> *speedrun-menu* 0 popup-menu) *speedrun-popup-menu*)
  (set! (-> *speedrun-menu* 0 draw-menu?) #f)
  (set! (-> *speedrun-menu* 0 ignore-menu-toggle?) #f)
  (set! (-> *speedrun-menu* 0 opened-with-start?) #f)
  (go idle)
  (none))

(defmethod deactivate ((this speedrun-menu))
  (set! *speedrun-menu* (the-as (pointer speedrun-menu) #f))
  ((method-of-type process-drawable deactivate) this)
  (none))

(defstate idle (speedrun-menu)
  :event (behavior ((proc process) (arg1 int) (event-type symbol) (event event-message-block))
    (case event-type
      (('invoke)
       (case (-> event param 0)
        (((speedrun-menu-command reset))
         (start-run! *speedrun-info*))
        (((speedrun-menu-command exit))
         (set-master-mode 'game)
         (set! (-> self draw-menu?) #f))
        (else
          (format 0 "nyi: invoke ~D~%" (-> event param 0))))))
    (the-as object 0))
  :trans (behavior ()
    (none))
  :code (behavior ()
    (until #f (suspend) )
    (none))
  :post (behavior ()
    (none)))

(defmethod draw! ((this speedrun-menu))
  ;; handle opening and closing the menu
  ;; allow the menu to be toggled again once one of the current buttons are released
  (when (and (-> this ignore-menu-toggle?)
             (or (not (cpad-hold? 0 l1)) (not (cpad-hold? 0 r1)))
             (or (and (-> this opened-with-start?) (not (cpad-hold? 0 start)))
                 (and (not (-> this opened-with-start?)) (not (cpad-hold? 0 select)))))
    (set! (-> this ignore-menu-toggle?) #f))
  (when (and (cpad-hold? 0 l1) (cpad-hold? 0 r1) (or (cpad-hold? 0 select) (cpad-hold? 0 start))
             (not (-> this ignore-menu-toggle?)))
    (cond
      ((= *master-mode* 'game)
       (set-master-mode 'menu)
       (set! (-> this draw-menu?) #t))
      ((= *master-mode* 'menu)
       (set-master-mode 'game)
       (set! (-> this draw-menu?) #f)))
    (cpad-clear! 0 l1 r1)
    ;; Track whether or not the menu was opened with start or select...
    (cond
      ((cpad-hold? 0 select)
       (cpad-clear! 0 select)
       (set! (-> this opened-with-start?) #f))
      ((cpad-hold? 0 start)
       (cpad-clear! 0 start)
       (set! (-> this opened-with-start?) #t)))
    (set! (-> this ignore-menu-toggle?) #t))
  (when (-> this draw-menu?)
    ;; handle any inputs for within the menu
    (cond
      ((cpad-pressed? 0 triangle select circle)
       (set! (-> this draw-menu?) #f))
      ((cpad-pressed? 0 up)
       (move-up! (-> this popup-menu)))
      ((cpad-pressed? 0 down)
       (move-down! (-> this popup-menu)))
      ((cpad-pressed? 0 x)
       (press! (-> this popup-menu))))
    ;; draw it
    (draw! (-> this popup-menu)))
  (none))


(define *tmp-continue-point* (new 'static 'continue-point
                             :name "tmp"
                             :level #f
                             :trans (new 'static 'vector :w 1.0)
                             :quat (new 'static 'vector :w 1.0)
                             :camera-trans (new 'static 'vector :w 1.0)
                             :camera-rot (new 'static 'inline-array vector3s 3)
                             :on-goto #f
                             :vis-nick #f
                             :want (new 'static 'inline-array level-buffer-state 6
                                    (new 'static 'level-buffer-state :name #f :display? #f :force-vis? #f :force-inside? #f)
                                    (new 'static 'level-buffer-state :name #f :display? #f :force-vis? #f :force-inside? #f)
                                    (new 'static 'level-buffer-state :name #f :display? #f :force-vis? #f :force-inside? #f)
                                    (new 'static 'level-buffer-state :name #f :display? #f :force-vis? #f :force-inside? #f)
                                    (new 'static 'level-buffer-state :name #f :display? #f :force-vis? #f :force-inside? #f)
                                    (new 'static 'level-buffer-state :name #f :display? #f :force-vis? #f :force-inside? #f))
                             :want-sound (new 'static 'array symbol 3 #f #f #f))
    )

(defun speedrun-practice-temp-checkpoint ()
  (when (-> *tmp-continue-point* level)
    (cond
      ;; (*tmp-flut?* (set! *practice-spawn-post-init* get-on-flutflut))
      ;; (*tmp-zoomer?* (set! *practice-spawn-post-init* get-on-zoomer))
      (else (set! *practice-spawn-post-init* generic-post-init))
      )
    ;; commented out to prevent malding 
    ;; (if (cpad-hold? 0 r2)  ;; fully reset game if r2 held too
    ;;   (initialize! *game-info* 'game (the-as game-save #f) "default")
    ;;   )
    (when (not (string= (-> *game-info* current-continue name) "tmp"))
      (set! *last-real-continue-point* (-> *game-info* current-continue))
      )
    (set! (-> *game-info* current-continue) *tmp-continue-point*)
    (cheats-sound-play #f)
    (initialize! *game-info* 'dead (the-as game-save #f) (the-as string #f))
    ;; (speedrun-reset-common-settings)
    )
  (none)
  )

(defun continue-point-copy! ((dst continue-point) (src continue-point))
  (set! (-> dst level) (or (-> src level) (-> *last-real-continue-point* level)))
  (set! (-> dst quat quad) (-> src quat quad))
  (set! (-> dst vis-nick) (-> src vis-nick))
  (dotimes (idx 6)
    ;; clear levels
    (set! (-> dst want idx name) #f)
    (set! (-> dst want idx display?) #f)

    (when (or (= (-> *level* level idx status) 'active)
              (= (-> *level* level idx status) 'loaded)
              (= (-> *level* level idx status) 'loading)
              )
      (set! (-> dst want idx name) (-> *level* level idx name))
      (set! (-> dst want idx display?) (-> *level* level idx display?))

      (when (name= (-> *level* level idx name) (-> dst level))
        (set! (-> dst want idx display?) 'display)
        (set! (-> dst want idx force-vis?) #t)
        (set! (-> dst want idx force-inside?) #t)
        )
      )
    )
  (dotimes (idx 3)
    (set! (-> dst want-sound idx)  (-> src want-sound idx))
    )
  (none)
  )

(defun camera-rot-copy! ((dst continue-point) (src matrix))
  (set! (-> dst camera-rot 0 x) (-> src vector 0 x))
  (set! (-> dst camera-rot 0 y) (-> src vector 0 y))
  (set! (-> dst camera-rot 0 z) (-> src vector 0 z))
  (set! (-> dst camera-rot 1 x) (-> src vector 1 x))
  (set! (-> dst camera-rot 1 y) (-> src vector 1 y))
  (set! (-> dst camera-rot 1 z) (-> src vector 1 z))
  (set! (-> dst camera-rot 2 x) (-> src vector 2 x))
  (set! (-> dst camera-rot 2 y) (-> src vector 2 y))
  (set! (-> dst camera-rot 2 z) (-> src vector 2 z))
  (none)
  )

(defun speedrun-practice-store-temp-checkpoint ()
  (case (-> *target* state)
    ;; only store temp checkpoint if in one of these states
    ((target-look-around target-stance target-wade-stance target-swim-stance)
      ;; assume current continue is good enough for the current location
      (continue-point-copy! *tmp-continue-point* (-> *game-info* current-continue))
      (vector-copy! (-> *tmp-continue-point* trans) (-> *target* root trans))
      (vector-copy! (-> *tmp-continue-point* quat) (-> *target* root quat))
      (vector-copy! (-> *tmp-continue-point* camera-trans) (-> *camera-combiner* trans))
      (camera-rot-copy! *tmp-continue-point* (-> *camera-combiner* inv-camera-rot))

      (cheats-sound-play #t)
      )
    )
  (none)
  )

(defun speedrun-practice-spawn ()
  ;; reset any previous post-target-init callback
  (set! *practice-spawn-post-init* #f)

  (speedrun-practice-temp-checkpoint)

  (none)
  )

